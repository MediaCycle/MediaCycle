# Tested with Torch3.1
# http://www.torch.ch/archives/Torch3src.tgz
PROJECT(Torch3)
cmake_minimum_required(VERSION 2.8)
FILE(GLOB TORCH_H */*.h)
FILE(GLOB TORCH_CC */*.cc)
FILE(GLOB TORCH_DIRS * )
FOREACH(TORCH_DIR ${TORCH_DIRS})
    IF(IS_DIRECTORY ${TORCH_DIR})
	INCLUDE_DIRECTORIES(${TORCH_DIR})
    ENDIF()
ENDFOREACH()
OPTION(USE_DOUBLE "double mode" ON)
IF(APPLE OR UNIX)
	SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wall -O2 -ffast-math")
	SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wall -O2 -ffast-math")
ENDIF()
IF(USE_DOUBLE)
	SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -DUSE_DOUBLE")
	SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -DUSE_DOUBLE")
ENDIF()
IF(UNIX AND NOT APPLE AND USE_DOUBLE)
	SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -malign-double")
	SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -malign-double")
ENDIF()
ADD_LIBRARY(torch_static STATIC ${TORCH_H} ${TORCH_CC})
ADD_LIBRARY(torch SHARED ${TORCH_H} ${TORCH_CC})
SET_TARGET_PROPERTIES(torch_static PROPERTIES OUTPUT_NAME "torch")
SET_TARGET_PROPERTIES(torch_static PROPERTIES PREFIX "lib")
INSTALL(TARGETS torch LIBRARY DESTINATION lib)
INSTALL(TARGETS torch_static ARCHIVE DESTINATION lib)
INSTALL(FILES ${TORCH_H} DESTINATION include/torch)

FILE(GLOB TORCH_EXAMPLE_CCS examples/*/*.cc )
FOREACH(TORCH_EXAMPLE_CC ${TORCH_EXAMPLE_CCS})
	GET_FILENAME_COMPONENT(TORCH_EXAMPLE ${TORCH_EXAMPLE_CC} NAME_WE)
	GET_FILENAME_COMPONENT(TORCH_EXAMPLE_PATH ${TORCH_EXAMPLE_CC} PATH)
	IF(NOT ${TORCH_EXAMPLE} MATCHES "speech_include")
		ADD_EXECUTABLE(${TORCH_EXAMPLE} ${TORCH_EXAMPLE_CC})
		TARGET_LINK_LIBRARIES(${TORCH_EXAMPLE} torch)
		INSTALL(TARGETS ${TORCH_EXAMPLE} RUNTIME DESTINATION share/bin)
		FILE(GLOB TORCH_EXAMPLE_DATA ${TORCH_EXAMPLE_PATH}/data)
		IF(TORCH_EXAMPLE_DATA)
			INSTALL(DIRECTORY ${TORCH_EXAMPLE_DATA} DESTINATION share)
		ENDIF()
	ENDIF()
ENDFOREACH()